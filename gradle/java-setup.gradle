//////////
// JAVA //
//////////
repositories { jcenter() }

// setup java
apply plugin: 'java'

sourceCompatibility = VER_JAVA
targetCompatibility = VER_JAVA
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

/////////////
// ECLIPSE //
/////////////
apply plugin: 'eclipse'
eclipse {
	classpath {
		downloadSources true
		downloadJavadoc true
	}
}
// always create fresh projects
tasks.eclipse.dependsOn(cleanEclipse)

apply plugin: 'com.diffplug.eclipse.resourcefilters'
eclipseResourceFilters {
	exclude().folders().name('build')
}

//////////////
// SPOTBUGS //
//////////////
apply plugin: 'com.github.spotbugs'
spotbugs {
	toolVersion = VER_SPOTBUGS
	ignoreFailures = false 	// bug free or it doesn't ship!
	reportsDir = file('build/spotbugs')
	effort = 'max'			// min|default|max
	reportLevel = 'medium'	// low|medium|high (low = sensitive to even minor mistakes)
	omitVisitors = []		// bugs that we want to ignore
}
tasks.named('spotbugsTest') {
	enabled = false
}
tasks.named('spotbugsMain') {
	// only run on Java 8 (no benefit to running twice)
	enabled = org.gradle.api.JavaVersion.current() == org.gradle.api.JavaVersion.VERSION_1_8
	reports {
		html.enabled = true
	}
}
dependencies {
	compileOnly 'net.jcip:jcip-annotations:1.0'
	compileOnly "com.github.spotbugs:spotbugs-annotations:${VER_SPOTBUGS}"
	compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}
